#include <Keypad.h> 
#include <LiquidCrystal_I2C.h>
#include <Servo.h>

// ====== LCD Setup ======
LiquidCrystal_I2C lcd(0x27, 16, 2);

// ====== Keypad Setup ======
const byte ROWS = 4;
const byte COLS = 4;
char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};

// Keypad wiring (rows and columns)
byte rowpins[ROWS] = {4, 5, 6, 7};     // R1–R4
byte colpins[COLS] = {8, 10, 11, 12};  // C1–C4
Keypad keypad = Keypad(makeKeymap(keys), rowpins, colpins, ROWS, COLS);

// ====== Servo Setup ======
Servo doorServo;
int servoPin = 9;   // Servo on pin 9

// ====== PIR Sensor and Buzzer ======
int pirPin = 2;     // PIR on pin 2
int buzzerPin = 3;  // Buzzer on pin 3

// ====== Password Setup ======
String password = "1234";   // Default password (change if you want)
String inputPassword = "";

// ====== State ======
bool motionDetected = false;

void setup() {
  lcd.init();
  lcd.backlight();
  Serial.begin(9600);

  pinMode(pirPin, INPUT);
  pinMode(buzzerPin, OUTPUT);
  digitalWrite(buzzerPin, LOW); // buzzer OFF at start

  doorServo.attach(servoPin);
  doorServo.write(0); // Locked position

  lcd.setCursor(0,0);
  lcd.print("Initializing");
  delay(2000);

  lcd.clear();
  lcd.print("Waiting for PIR...");
}

void loop() {
  int motion = digitalRead(pirPin);

  if (motion == HIGH && !motionDetected) {
    motionDetected = true;
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("Motion Detected");
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("Enter Password:");
    lcd.setCursor(0,1);
    inputPassword = "";
  }

  if (motionDetected) {
    char key = keypad.getKey();
    if (key) {
      if (key == '#') {
        checkPassword();
      } else if (key == '*') {
        inputPassword = "";
        lcd.setCursor(0,1);
        lcd.print("Cleared       ");
        delay(500);
        lcd.setCursor(0,1);
        lcd.print("Enter Again:");
      } else {
        inputPassword += key;
        lcd.setCursor(0,1);
        lcd.print(inputPassword);
      }
    }
  }
}

// ====== Functions ======
void checkPassword() {
  if (inputPassword == password) {
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("Access Granted");
    digitalWrite(buzzerPin, LOW); 
    doorServo.write(90); // Unlock
    delay(5000);
    doorServo.write(0);  // Lock again
    lcd.setCursor(0,1);
    lcd.print("Door Locked");
    delay(2000);
    lcd.clear(); // Clear LCD after task
    lcd.print("Waiting for PIR...");
    motionDetected = false;
  } else {
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("Access Denied!");
    digitalWrite(buzzerPin, HIGH); // buzzer ON
    delay(3000);
    digitalWrite(buzzerPin, LOW);  // buzzer OFF
    lcd.setCursor(0,1);
    lcd.print("Try Again...");
    delay(2000);
    lcd.clear(); // Clear LCD after task
    lcd.print("Waiting for PIR...");
    motionDetected = false;
  }
}
